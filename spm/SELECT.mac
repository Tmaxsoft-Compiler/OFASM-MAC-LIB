         MACRO
&NAME   SELECT &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,&P11,&P12,     *
               &P13,&P14,&P15,&P16,&P17,&P18,&P19,&P20,&FLD=,&C=
.*#-------------------------------------------------------------------*
.*#      SELECT MACRO FOR STRUCTURED PROGRAMMING                      *
.*#      TMAX ADD                                                     *
.*#-------------------------------------------------------------------*
         LCLA  &I              INDEX FOR STRING SCANNING
         LCLA  &N              TOTAL NESTING LEVEL
         LCLC  &OPND
         COPY  IFGLO
.*--------------------------------------------------------------------*
.*       FIRST IF: INIT GLOBALS                                       *
.*--------------------------------------------------------------------*
         AIF   (&IFINIT).START            INIT ALREADY DONE
&IFINIT  SETB  1
&IFLEVEL SETA  0
&DOLEVEL SETA  0
&IFLABEL SETA  0
&IFLIMIT SETA  100000
&IFPRAEF SETC  '##'
&IFDEBUG SETB  0
.* TMAX ADD FOR SELECT
&SELLEVEL SETA 0
&SELLABEL SETA 0
.*
.START   ANOP
&MACNA   SETC  'SELECT'
.*--------------------------------------------------------------------*
.*       INCREMENT LEVEL. GENERATE LABELS FOR FALSE/TRUE              *
.*--------------------------------------------------------------------*
&SELLEVEL SETA &SELLEVEL+1
         AIF   (&SELLEVEL EQ 50).FEHL06
         AIF   ('&SYSPARM' EQ '').OBR00
         AIF   ('&SYSPARM'(1,2) EQ 'NO').OBR00
         AGO     .OBR00
&NAME    SVDOC   COM=START,C=&C,                                       *
               &P1,&P2,&P3,&P4,&P5,&P6,&P7,&P8,&P9,&P10,&P11,&P12,&P13,*
               &P14,&P15,&P16,&P17,&P18,&P19,&P20
.*
.OBR00   ANOP
&SELLABEL SETA &SELLABEL+1
         AIF   (&SELLABEL GE &IFLIMIT).FEHL06
&SELFALSE(&SELLEVEL) SETC '&SELLABEL'
&SELTRUE SETC  ''
.*
         AIF   ('&SYSLIST(1)' EQ '').STA03 
         AIF   ('&FLD' NE '').STA06
         AIF   (N'&SYSLIST GT 5).FEHL08
         AIF   (N'&SYSLIST LT 3).FEHL09
         AIF   (N'&SYSLIST EQ 4).STA04
         AGO   .STA05
.*
.*
.STA03   ANOP
.*
         AIF   ('&SELTRUE' EQ '').MACEND
&OPND    SETC  '&IFPRAEF&SELTRUE'
&OPND    DS    0H                        TARGET FOR BANCH ON NOT TRUE
         AGO   .MACEND
.*
.STA04   ANOP
.* SYSLIST=4
&SELCMP(&SELLEVEL) SETC '&SYSLIST(4)'
&SELOP(&SELLEVEL) SETC '&SYSLIST(2),&SYSLIST(3),&SYSLIST(4)'
         AGO   .STA08
.*
.STA05   ANOP
.* SYSLIST=3
&SELCMP(&SELLEVEL) SETC '&SYSLIST(3)'
&SELOP(&SELLEVEL) SETC '&SYSLIST(2),&SYSLIST(3)'
         AGO   .STA08
.*
.STA06   ANOP
.* FLD=
&SELOP(&SELLEVEL) SETC '&FLD'
         AIF   ('&SYSLIST(2)' EQ '').STA07 
&SELCMP(&SELLEVEL) SETC '&SYSLIST(2)'
&SELOP(&SELLEVEL) SETC '&SELOP(&SELLEVEL),&SYSLIST(2)'
         AGO   .STA08
.*
.STA07   ANOP
&SELCMP(&SELLEVEL) SETC 'EQ'
&SELOP(&SELLEVEL) SETC '&SELOP(&SELLEVEL),&SELCMP(&SELLEVEL)'
.*
.STA08   ANOP
.*INSTRUCTION
&SELOP(&SELLEVEL) SETC '&SYSLIST(1),&SELOP(&SELLEVEL)'
         AGO   .MACEND
.*
.*--------------------------------------------------------------------*
.*
.*         COPY IFERR
.* TMAX ADD FOR COPY IFERR 
.*#-------------------------------------------------------------------*
.*# IFERR: ERROR MESSAGES FOR STRUCTURED PROGRAMMING-MACROS           *
.*#-------------------------------------------------------------------*
.FEHL01  MNOTE 8,'IFINIT MACRO NOT FIRST MACRO'
         AGO   .EREND
.FEHL02  MNOTE 8,'PRAEFIX BIGGER THAN 7 POSITIONS'
         AGO   .EREND
.FEHL03  MNOTE 8,'DEBUG NOT YES OR NO'
         AGO   .EREND
.FEHL04  MNOTE 8,'TRACE SWITCH NOT YES OR NO'
         AGO   .EREND
.FEHL05  MNOTE 8,'COMPARE CODE NOT IN BRACKETS'
         AGO   .EREND
.FEHL06  MNOTE 8,'TOO MUCH STRUCTURED MACROS IN THIS PROGRAM'
         AGO   .EREND
.FEHL07  MNOTE 8,'SHOULD NOT OCCUR'
         AGO   .EREND
.FEHL08  MNOTE 8,'TOO MUCH OPERANDS IN CONDITION'
         AGO   .EREND
.FEHL09  MNOTE 8,'NOT ENOUGH OPERANDS IN CONDITION'
         AGO   .EREND
.FEHL10  MNOTE 8,'AND/OR CONTINATION MISSING'
         AGO   .EREND
.FEHL11  MNOTE 8,'INVALID COMPARE OPERANDS'
         AGO   .EREND
.FEHL12  MNOTE 8,'FROM AND WHILE ARE EXCLUSIVE'
         AGO   .EREND
.FEHL13  MNOTE 8,'WHILE PARAMETER NOT IN BRACKETS'
         AGO   .EREND
.FEHL14  MNOTE 8,'EXCESSIVE PARAMETERS IN DO STATEMENT'
         AGO   .EREND
.FEHL15  MNOTE 8,'INVALID PARAMETER'
         AGO   .EREND
.FEHL16  MNOTE 8,'EXIT NEEDS CONDITION'
         AGO   .EREND
.FEHL17  MNOTE 8,'PARAMETER MISSING'
         AGO   .EREND
.FEHL18  MNOTE 8,'INVALID OPERAND IN DO STATEMENT'
         AGO   .EREND
.FEHL19  MNOTE 8,'DONAME NOT DEFINED'
         AGO   .EREND
.* TMAX ADD FOR UNTIL
.FEHL20  MNOTE 8,'FROM AND UNTIL ARE EXCLUSIVE'
         AGO   .EREND
.EREND   ANOP
         MEXIT
.*--------------------------------------------------------------------*
.*
.MACEND  ANOP
         MEXIT
         MEND
